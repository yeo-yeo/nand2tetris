// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=o1);
    Mux16(a=y,b=false,sel=zy,out=o2);
    Not16(in=o1,out=o3); // becomes -1 not 1??
    Not16(in=o2,out=o4); // likewise
    Mux16(a=o1,b=o3,sel=nx,out=o5);
    Mux16(a=o2,b=o4,sel=ny,out=o6);
    And16(a=o5,b=o6,out=o7); // f0 is and
    Add16(a=o5,b=o6,out=o8); // f1 is add
    Mux16(a=o7,b=o8,sel=f,out=o9);
    Not16(in=o9,out=o10);
    Mux16(a=o9,b=o10,sel=no,out[0..7]=outLeft,out[8..15]=outRight,out[15]=ng,out=out);
    Or8Way(in=outLeft,out=o11);
    Or8Way(in=outRight,out=o12);
    Or(a=o11,b=o12,out=o13);
    Not(in=o13,out=zr);
}